1000 '= Put Line-Num to
1010 '=  Numberless BASIC Source
1020 '=           &
1030 '= Convert Labels into Line-Num
1040 '=     Ver 1.4.0  (c) 1993,1995,2019 molety
1050 '
1060 '== INIT
1070 MAXFILES=2:CLEAR5000
1080 ONERRORGOTO3410
1090 ONSTOPGOSUB3760:STOPON
1100 SCREEN0:WIDTH40:KEYOFF:CLS
1110 DEFINTA-Z
1120 A=0:N!=0:I=0:U=0
1130 SL!=0:LN!=0:IL=0
1140 SW=0:P1=0:P2=0:P3=0:P4=0:P5=0:P6=0
1150 LB=0:DO=0:SP=0:SF=0
1160 A$="":K$="":QT$=CHR$(34)
1170 IL$="":HD$="":L1$="":L2$="":I2$=""
1180 DF$="":M$=""
1190 DIM SF$(14)
1200 DIM LB!(255),LB$(255)
1210 DIM DO!(99,2),DO$(99)
1220 DIM ST(19),S2(19,1)
1230 DIM C1$(5),C2$(13),PT(255,1)
1240 DEFUSR=&H156
1250 FOR A=0 TO 5:READ C1$(A):IF C1$(A)="" THENC1$(A)=QT$
1260 NEXT
1270 FOR A=0 TO 13:READ C2$(A):NEXT
1280 DEFFNS$(N!)=MID$(STR$(N!),2)
1290 LOCATE10,0:PRINT"Put Line Numbers &"
1300 LOCATE13,1:PRINT"Convert Labels"
1310 '== DATA INPUT
1320 LOCATE1,3:PRINT"Number of Source Files:";
1330 GOSUB3330
1340 IF N!<1 OR N!>15 THENBEEP:GOTO1320
1350 SF=N!-1
1360 FOR I=0 TO SF
1370 LOCATE1,I+5:PRINT"Source File";FNS$(I+1);SPC(6+(I>8));":";
1380 GOSUB3250:GOSUB3130:SF$(I)=A$
1390 NEXT
1400 LOCATE1,SF+7:PRINT"Destination File  :";
1410 GOSUB3250:GOSUB3130:DF$=A$
1420 A=0
1430 IF SF$(A)=DF$ THENBEEP:GOTO1320
1440 IF A<SF THENA=A+1:GOTO1430
1450 LOCATE1,SF+9:PRINT"Start Line Number :";
1460 GOSUB3330:SL!=N!:LN!=SL!
1470 CLS
1480 '== PASS-1 (REGISTER LABELS)
1490 PRINT">>> PASS-1"
1500 FOR I=0 TO SF:IL=1
1510 PRINT"Module";I+1
1520 OPEN SF$(I) FORINPUTAS#1
1530 IF EOF(1) THEN1990
1540 PRINT" Line";IL
1550 LINEINPUT#1,IL$
1560 A$=IL$:GOSUB3070:IL$=A$
1570 A$=LEFT$(IL$,4):GOSUB3130:HD$=A$
1580 IF HD$="" THEN1630
1590 IF LEFT$(HD$,1)="*" THENGOSUB1650:GOTO1630
1600 IF LEFT$(HD$,2)="DO" THENGOSUB1760:GOTO1630
1610 IF HD$="LOOP" THENGOSUB1870
1620 GOSUB3180
1630 IL=IL+1:GOTO1530
1640 '=== LABEL
1650 L1$="":L2$=""
1660 A$=IL$:GOSUB3100:GOSUB3130:IL$=A$
1670 IL$=MID$(IL$,2):IF IL$="" THEN1720
1680 L1$=LEFT$(IL$,1)
1690 IF L1$="_" OR (L1$>="0" AND L1$=<"9") OR (L1$>="A" AND L1$=<"Z") THENL2$=L2$+L1$:GOTO1670
1700 ERROR82
1710 '====
1720 IF L2$="" THEN ERROR82
1730 LB!(LB)=LN!:LB$(LB)=L2$
1740 LB=LB+1:RETURN
1750 '=== DO
1760 DO!(DO,1)=LN!:ST(SP)=DO:S2(SP,0)=I:S2(SP,1)=IL:SP=SP+1
1770 A$=MID$(IL$,3):GOSUB3070:IL$=A$
1780 IF IL$="" THEN DO!(DO,0)=0:GOTO1850
1790 A$=LEFT$(IL$,5):GOSUB3130:L1$=A$
1800 IF L1$<>"WHILE" AND L1$<>"UNTIL" THEN ERROR83
1810 IF L1$="WHILE" THEN DO!(DO,0)=1 ELSE DO!(DO,0)=2
1820 A$=MID$(IL$,6):GOSUB3070:GOSUB3100:IL$=A$
1830 DO$(DO)=IL$
1840 GOSUB3180
1850 DO=DO+1:RETURN
1860 '=== LOOP
1870 SP=SP-1:IF SP<0 THEN ERROR84
1880 DO!(ST(SP),2)=LN!
1890 A$=MID$(IL$,5):GOSUB3070:IL$=A$
1900 IF IL$="" THEN1970
1910 A$=LEFT$(IL$,5):GOSUB3130:L1$=A$
1920 IF L1$<>"WHILE" AND L1$<>"UNTIL" THEN ERROR83
1930 IF DO!(ST(SP),0)>0 THEN ERROR85
1940 IF L1$="WHILE" THEN DO!(ST(SP),0)=3 ELSE DO!(ST(SP),0)=4
1950 A$=MID$(IL$,6):GOSUB3070:GOSUB3100:IL$=A$
1960 DO$(ST(SP))=IL$
1970 RETURN
1980 '===
1990 CLOSE#1:NEXT I
2000 IF SP>0 THEN I=S2(SP-1,0):IL=S2(SP-1,1):ERROR86
2010 PRINT"Registered Label(s)"
2020 IF LB=0 THENPRINT"None.":GOTO2050
2030 FOR A=0 TO LB-1:PRINT"*"+LB$(A):NEXT
2040 '== PASS-2 (CONVERT & WRITE)
2050 PRINT">>> PASS-2"
2060 LN!=SL!:DO=0:SP=0
2070 OPEN DF$ FOROUTPUTAS#2
2080 FOR I=0 TO SF:IL=1
2090 PRINT"Module";I+1
2100 OPEN SF$(I) FORINPUTAS#1
2110 IF EOF(1) THEN2930
2120 PRINT" Line";IL
2130 LINEINPUT#1,IL$
2140 A$=IL$:GOSUB3070:IL$=A$
2150 A$=LEFT$(IL$,4):GOSUB3130:HD$=A$
2160 IF HD$="" THEN2210
2170 IF LEFT$(HD$,1)="*" THEN2210
2180 IF LEFT$(HD$,2)="DO" THENGOSUB2230:GOTO2210
2190 IF HD$="LOOP" THENGOSUB2310 ELSEGOSUB2380
2200 GOSUB3220:GOSUB3180
2210 IL=IL+1:GOTO2110
2220 '=== DO
2230 ST(SP)=DO:SP=SP+1
2240 IF DO!(DO,0)=0 OR DO!(DO,0)>=3 THEN2290
2250 IL$="IF "+DO$(DO)+" THEN"
2260 IF DO!(DO,0)=1 THENIL$=IL$+"ELSE"
2270 IL$=IL$+" "+FNS$(DO!(DO,2)+10)
2280 GOSUB3220:GOSUB3180
2290 DO=DO+1:RETURN
2300 '=== LOOP
2310 SP=SP-1
2320 IF DO!(ST(SP),0)=<2 THENIL$="GOTO":GOTO2350
2330 IL$="IF "+DO$(ST(SP))+" THEN"
2340 IF DO!(ST(SP),0)=4 THENIL$=IL$+"ELSE"
2350 IL$=IL$+" "+FNS$(DO!(ST(SP),1))
2360 RETURN
2370 '=== ORDINARY STATEMENTS
2380 IF INSTR(IL$,"*")=0 THENRETURN
2390 A$=IL$:GOSUB3130:I2$=A$
2400 '==== PICK UP WORDS
2410 P1=0
2420 FOR A=0 TO 5:P2=1
2430 P3=INSTR(P2,I2$,C1$(A))
2440 IF P3 THENPT(P1,0)=P3:PT(P1,1)=A:P1=P1+1:P2=P3+1:GOTO2430
2450 NEXT
2460 '==== SORT
2470 IF P1<2 THEN2530
2480 SW=0
2490 FOR A=0 TO P1-2
2500 IF PT(A,0)>PT(A+1,0) THENSWAP PT(A,0),PT(A+1,0):SWAP PT(A,1),PT(A+1,1):SW=1
2510 NEXT:IF SW THEN2480
2520 '==== SEARCH LABEL
2530 P2=0:P3=1:SW=0
2540 IF PT(P2,1)=0 THEN SW=3-SW
2550 IF PT(P2,1)=1 AND SW=0 THENRETURN
2560 IF PT(P2,1)=2 AND SW=0 THENRETURN
2570 IF PT(P2,1)=3 AND SW=0 THEN SW=1
2580 IF PT(P2,1)=4 AND SW=1 THEN SW=0
2590 IF PT(P2,1)=5 AND SW=0 THENGOSUB2630
2600 P3=PT(P2,0)+1:P2=P2+1:IF P2<P1 THEN2540
2610 RETURN
2620 '===== CHECK
2630 P4=PT(P2,0)
2640 A$=MID$(I2$,P3,P4-P3):GOSUB3100:L1$=A$
2650 A=0
2660 P5=1
2670 P6=INSTR(P5,L1$,C2$(A))
2680 IF P6=0 THEN2710
2690 IF P6=LEN(L1$)-LEN(C2$(A))+1 THEN2740
2700 P5=P6+LEN(C2$(A)):GOTO2670
2710 IF A<13 THENA=A+1:GOTO2660
2720 RETURN
2730 '===== IDENTIFY
2740 L1$=MID$(I2$,P4+1):L2$=""
2750 A=1
2760 A$=MID$(L1$,A,1)
2770 IF A$="_" OR (A$>="0" AND A$=<"9") OR (A$>="A" AND A$=<"Z") THENL2$=L2$+A$:A=A+1:GOTO2760
2780 A=0
2790 IF L2$=LB$(A) THEN2830
2800 A=A+1:IF A<LB THEN2790
2810 ERROR87
2820 '===== CONVERT
2830 L1$=LEFT$(IL$,P4-1)+FNS$(LB!(A))+MID$(IL$,P4+LEN(L2$)+1)
2840 I2$=LEFT$(I2$,P4-1)+FNS$(LB!(A))+MID$(I2$,P4+LEN(L2$)+1)
2850 P2=P2+1:IF P2<P1 AND PT(P2,0)<P4+LEN(L2$)+1 THEN2850
2860 IF P2=P1 THEN2910
2870 P5=LEN(L1$)-LEN(IL$)
2880 FOR A=P2 TO P1-1
2890 PT(A,0)=PT(A,0)+P5
2900 NEXT
2910 P2=P2-1:IL$=L1$:RETURN
2920 '===
2930 CLOSE#1:NEXT I
2940 CLOSE#2
2950 PRINT"Completed.":BEEP
2960 '== QUIT
2970 PRINT"A) AGAIN"
2980 PRINT"B) BASIC"
2990 PRINT"D) DOS"
3000 K$=INKEY$:IF K$="" THEN3000
3010 IF INSTR("Aa",K$) THENRUN
3020 IF INSTR("Bb",K$) THENONERRORGOTO0:END
3030 IF INSTR("Dd",K$) THEN_SYSTEM
3040 GOTO3000
3050 '= SUB ROUTINE
3060 '== REMOVE LEFT'S SPACE
3070 IF LEFT$(A$,1)=" " THENA$=MID$(A$,2):GOTO3070
3080 RETURN
3090 '== REMOVE RIGHT'S SPACE
3100 IF RIGHT$(A$,1)=" " THENA$=LEFT$(A$,LEN(A$)-1):GOTO3100
3110 RETURN
3120 '== CHANGE SMALL==>CAPITAL
3130 IF A$="" THENRETURN
3140 FOR A=1 TO LEN(A$)
3150 K$=MID$(A$,A,1):IF K$>="a" AND K$=<"z" THENMID$(A$,A,1)=CHR$(ASC(K$)-32)
3160 NEXT:RETURN
3170 '== LINE NUMBER INCREMENT
3180 LN!=LN!+10
3190 IF LN!>65529! THENIL=IL+1:ERROR81
3200 RETURN
3210 '== WRITE 1 LINE
3220 PRINT#2,FNS$(LN!)+" "+IL$
3230 RETURN
3240 '== INPUT FILENAME
3250 U=USR(0):A$=""
3260 PRINTA$;SPC(16-LEN(A$));STRING$(16,29);
3270 K$=INKEY$:IF K$="" THEN3270
3280 IF K$=CHR$(13) AND A$<>"" THENRETURN
3290 IF K$=CHR$(8) AND A$<>"" THENA$=LEFT$(A$,LEN(A$)-1)
3300 IF INSTR(QT$+"*+,/;=?[]",K$)=0 AND ASC(K$)>32 AND ASC(K$)<>127 AND LEN(A$)<16 THENA$=A$+K$
3310 GOTO3260
3320 '== INPUT NUMBER
3330 U=USR(0):A$=""
3340 PRINTA$;SPC(5-LEN(A$));STRING$(5,29);
3350 K$=INKEY$
3360 IF K$=CHR$(13) AND A$<>"" AND VAL(A$)<65530! THENN!=VAL(A$):RETURN
3370 IF K$=CHR$(8) AND A$<>"" THENA$=LEFT$(A$,LEN(A$)-1)
3380 IF K$>="0" AND K$=<"9" AND LEN(A$)<5 THENA$=A$+K$
3390 GOTO3340
3400 '= ERROR
3410 CLOSE:M$=""
3420 IF ERR=81 THENM$="Destination Line Number is over 65529"
3430 IF ERR=82 THENM$="Bad Label Name"
3440 IF ERR=83 THENM$="Syntax Error of DO or LOOP"
3450 IF ERR=84 THENM$="LOOP without DO"
3460 IF ERR=85 THENM$="Both DO and LOOP have Condition Formulas"
3470 IF ERR=86 THENM$="DO without LOOP"
3480 IF ERR=87 THENM$="Undefined Label"
3490 IF ERR=15 THENM$="String Too Long"
3500 IF M$<>"" THENM$=M$+" in Module"+FNS$(I+1)+" Line"+FNS$(IL):GOTO3640
3510 '
3520 IF ERR=14 THENM$="Out of String Space."
3530 IF ERR=53 THENM$="Source File is not Found."
3540 IF ERR=60 THENM$="Bad FAT."
3550 IF ERR=62 THENM$="Bad Drive Name."
3560 IF ERR=66 THENM$="Disk Full."
3570 IF ERR=67 THENM$="Too Many Files."
3580 IF ERR=68 THENM$="Disk Write Protected."
3590 IF ERR=69 THENM$="Disk I/O Error."
3600 IF ERR=70 THENM$="Disk Offline."
3610 '
3620 IF ERR= 9 THENGOSUB3670
3630 IF ERR=56 THENGOSUB3720
3640 IF M$<>"" THENPRINTM$:BEEP:RESUME2970
3650 ONERRORGOTO0:END
3660 '== SUBSCRIPT OUT OF RANGE
3670 IF LB>255 THENM$="Too Many Labels."
3680 IF DO>99 THENM$="Too Many DO-LOOP."
3690 IF SP>19 THENM$="DO-LOOP Stack Overflow."
3700 RETURN
3710 '== BAD FILE NAME
3720 IF ERL=2070 THENM$="Destination" ELSEM$="Source"
3730 M$="Bad "+M$+" Filename."
3740 RETURN
3750 '= CTRL+STOP
3760 CLOSE:ONERRORGOTO0:BEEP:END
3770 '= CHECK WORD'S DATA
3780 '== C1$()
3790 DATA ,"'","REM","DATA",":","*"
3800 '== C2$()
3810 DATA "GOTO","GO TO","GOSUB","RETURN","RESUME","RESTORE","THEN","ELSE","RUN","LIST","DELETE","-","=",","
